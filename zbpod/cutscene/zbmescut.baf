/* 
  cutscene where nephetel and the vampire meet in the sewers  
  we'll need to add 'playsound' for each line once we have the VO lines
  right now we only have the one vampire sound
  ideally played AFTER the player has explored the sewers but yolo 
  fires AFTER nephetel / mysterious messenger gives charname the note
                                                                     */ 
IF
	True() 
	Global("zbnephcutscene","GLOBAL",0) /* we haven't seen the cutscene */ 
THEN
	RESPONSE #100
		CutSceneID(Player1) /* who performs the action */
		/* actions start here */ 
		SetGlobal("zbnephcutscene","GLOBAL",1) /* we don't want the cutscene to repeat */
		Wait(2)
		DisplayStringHead(Player1,@1800013)
		Wait(2)
		PlaySound("MISC_03A")
		PlayDead(45)
		FadeToColor([50.0],0)
		Wait(2)
		StorePartyLocations()
		LeaveAreaLUA("AR0701","",[2034.873],NW) /* carries player over */ 
		ActionOverride(Player2,LeaveAreaLUA("AR0701","",[2034.873],NW))  
		ActionOverride(Player3,LeaveAreaLUA("AR0701","",[2034.873],NW))  
		ActionOverride(Player4,LeaveAreaLUA("AR0701","",[2034.873],NW))  
		ActionOverride(Player5,LeaveAreaLUA("AR0701","",[2034.873],NW))  
		ActionOverride(Player6,LeaveAreaLUA("AR0701","",[2034.873],NW))  /* carries party over, they have to be out of sight */ 
		CreateCreature("zspdnf16",[1560.680],NE)  /* spawns in nephetel */ 
		CreateCreature("zbvamp",[1670.680],NE)  /* spawns in vampire */ 
		SetAreaScript("zbnephskip",OVERRIDE)
		SetGlobal("ZBNEPH_CUTSCENE_BREAKABLE","GLOBAL",1)
		SetCutSceneBreakable(TRUE)
		Wait(2)
		FadeFromColor([30.0],0)
		MoveViewPoint([1670.680],INSTANT)
		ActionOverride("zspdnf16",FaceObject("zbvamp"))
		ActionOverride("zbvamp",FaceObject("zspdnf16"))
		Wait(3)
		DisplayStringHead("zbvamp",@1800000)  /* ~Have you found any information about the book?~ */ 
		Wait(5)
		DisplayStringHead("zspdnf16",@1800001)  /* ~No. All anyone can talk about is this Hero of Baldur’s Gate.~ */ 
		Wait(5)
		DisplayStringHead("zspdnf16",@1800002) /* ~Every tavern or noble house I scout there is someone who is saying they saved this or defeated that.~ */ 
		Wait(5)
		DisplayStringHead("zspdnf16",@1800003) /* ~Quite annoying.~ */ 
		Wait(5)
		ActionOverride("zbvamp",MoveToPoint([1670.600])) /* pacing north */ 
		Wait(3)
		ActionOverride("zbvamp",MoveToPoint([1670.752])) /* pacing south */ 
		Wait(3)
		DisplayStringHead("zbvamp",@1800004) /* ~Who are they? What do you know about them?~ */ 
		Wait(5)
		DisplayStringHead("zspdnf16",@1800005) /* ~They say they are a Bhaalspawn. One of the children of the Lord of Murder.~ */ 
		Wait(5)
		ActionOverride("zbvamp",MoveToPoint([1670.680])) /* pacing back to usual spot */ 
		ActionOverride("zbvamp",FaceObject("zspdnf16"))
		DisplayStringHead("zbvamp",@1800006) /* ~Is it anything like the weapon in the Pits?~ */ 
		Wait(5)
		ActionOverride("zspdnf16",Face(W)) /* nephetel faces away */  
		Wait(2)
		DisplayStringHead("zspdnf16",@1800007) /* ~No, not like Enegar. They are free of the Cult and choose of their own free will.~ */ 
		Wait(5)
		ActionOverride("zspdnf16",FaceObject("zbvamp")) /* she turns back */ 
		Wait(2)
		DisplayStringHead("zspdnf16",@1800008) /* ~No one has spoken as to why, but I will investigate.~ */ 
		Wait(5)
		ActionOverride("zbvamp",SwingOnce()) /* quick attack animation */ 
		Wait(2)
		DisplayStringHead("zbvamp",@1800009) /* ~Good. Good. I know you will find the Book.~ */ 
		Wait(5)
		DisplayStringHead("zbvamp",@1800010) /* ~Our Master will be very… very pleased, he has assured me.~ */ 
		Wait(5)
		DisplayStringHead("zbvamp",@1800011) /* ~I expect a full report on this 'Hero' when next we meet.~ */ 
		PlaySound("VAMPFX07") /* plays the vampire sound */ 
		Wait(5)
		ActionOverride("zspdnf16",MoveToPoint([1560.1069])) /* neph moving south */ 
		Wait(2)
		ActionOverride("zbvamp",FaceObject("zspdnf16"))
		Wait(2)
		ActionOverride("zspdnf16",FaceObject("zbvamp"))
		Wait(2)
		DisplayStringHead("zspdnf16",@1800012) /* ~Of course, Spymaster.~ */ 
		Wait(5)		
		FadeToColor([50.0],0)
		SetCutSceneBreakable(FALSE)
		SetGlobal("ZBNEPH_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		ActionOverride("zspdnf16",DestroySelf())
		Wait(1)
		ActionOverride("zbvamp",DestroySelf())
		Wait(1)
		RestorePartyLocations()
		/* actions end here */ 
		EndCutSceneMode() /* exits the the StartCutSceneMode() action */
		Wait(1)
		FadeFromColor([50.0],0)
		AddJournalEntry(@800001,QUEST) /* add journal entry, moved over from d file */ 
		GiveItemCreate("zbpdnote",Player1,1,0,0) /* give player the note, moved over from d file */ 
END
