BACKUP "weidu_external/zbpod/backup/"
SUPPORT "https://www.gibberlings3.net/forums/forum/28-miscellaneous-released-mods/"
VERSION "0.1"
README ~%MOD_FOLDER%/README.html~
AUTO_EVAL_STRINGS
/////////////////////////////////////////////////////////////////////////////////////////////////////

AUTO_TRA ~%MOD_FOLDER%/lang/%s~
ALWAYS
  // Workspace
  OUTER_SPRINT workspace "weidu_external/zbpod/workspace"
  MKDIR "%workspace%"
  INCLUDE ~%MOD_FOLDER%/lib/a7#add_kit_ex.tpa~
  INCLUDE ~%MOD_FOLDER%/lib/alter_script.tpa~
  INCLUDE ~%MOD_FOLDER%/lib/functions.tph~
  INCLUDE ~%MOD_FOLDER%/lib/royalprotector/zs_royalfunctions.tpa~
  INCLUDE ~%MOD_FOLDER%/lib/ds/ds.tph~ // Detectable Spells
END


AUTO_TRA ~%MOD_FOLDER%/lang/%s~
LANGUAGE  ~English~
          ~english~
          ~zbpod/lang/english/game.tra~
          ~zbpod/lang/english/weidu.tra~
          ~zbpod/lang/english/dialogs.tra~
          ~zbpod/lang/english/JournalEntries.tra~
          ~zbpod/lang/english/cutscenes.tra~

LANGUAGE  ~Simplified Chinese By Lzw104522773~
          ~schinese~
          ~zbpod/lang/schinese/game.tra~
          ~zbpod/lang/schinese/weidu.tra~
          ~zbpod/lang/schinese/dialogs.tra~
          ~zbpod/lang/schinese/JournalEntries.tra~
          ~zbpod/lang/schinese/cutscenes.tra~


//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @20001 DESIGNATED 2 LABEL ~zb_pod_ee~ // INSTALL PIT OF DESPAIR FOR ENHANCED EDITION
REQUIRE_PREDICATE GAME_IS "bg2ee eet" @20050
//////////////////////////////////////////////////////////////////////////////////////////////////////

// Add new areas
// Script taken from IWD1_EET, for cleaning the area
COPY_EXISTING ~AR0701.ARE~ ~override/ZB0701.ARE~
READ_LONG   0x54 actor_off
READ_SHORT  0x58 actor_num
PATCH_IF (~%actor_num%~ > 0) BEGIN
  WRITE_SHORT 0x58 0
  READ_LONG   0x5c triggers_off
  PATCH_IF (~%triggers_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x5c ~%triggers_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x60 spawn_off
  PATCH_IF (~%spawn_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x60 ~%spawn_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x68 entrance_off
  PATCH_IF (~%entrance_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x68 ~%entrance_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x70 contain_off
  PATCH_IF (~%contain_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x70 ~%contain_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x78 item_off
  PATCH_IF (~%item_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x78 ~%item_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x7c vert_off
  PATCH_IF (~%vert_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x7c ~%vert_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x84 ambi_off
  PATCH_IF (~%ambi_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x84 ~%ambi_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0x88 var_off
  PATCH_IF (~%var_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0x88 ~%var_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xa0 explore_off
  PATCH_IF (~%explore_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xa0 ~%explore_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xa8 door_off
  PATCH_IF (~%door_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xa8 ~%door_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xb0 anim_off
  PATCH_IF (~%anim_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xb0 ~%anim_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xb8 tiled_off
  PATCH_IF (~%tiled_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xb8 ~%tiled_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xbc song_off
  PATCH_IF (~%song_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xbc ~%song_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xc0 rest_enc_off
  PATCH_IF (~%rest_enc_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xc0 ~%rest_enc_off%~ - ~%actor_num%~ * 0x110
  END
  READ_LONG   0xc4 auto_map_off
  PATCH_IF (~%auto_map_off%~ >= ~%actor_off%~) BEGIN
    WRITE_LONG 0xc4 ~%auto_map_off%~ - ~%actor_num%~ * 0x110
  END
  DELETE_BYTES ~%actor_off%~ ~%actor_num%~ * 0x110
END

APPEND ~MASTAREA.2da~ ~ZB0701     value      ~
UNLESS ~ZB0701~

// Copy spells and movies
COPY ~%MOD_FOLDER%/spells~ ~override~
COPY ~%MOD_FOLDER%/spells/zbantisb.bam~ ~override/zbantisc.bam~
// Patch in portrait icon
OUTER_SET description = RESOLVE_STR_REF(@18000)
LAUNCH_ACTION_FUNCTION cd_new_portrait_icon INT_VAR description = description STR_VAR bam_file="zbantisb" END
COPY ~%MOD_FOLDER%/movie/pod.wbm~ ~movies~ /* adding directly to movies, not override, because otherwise it doesn't work. this isn't ideal. */

ACTION_IF NOT FILE_EXISTS_IN_GAME ~p_cta1.bam~ THEN BEGIN
INCLUDE ~%MOD_FOLDER%/lib/animations.tpa~
END

INCLUDE ~%MOD_FOLDER%/lib/fragments/mo_initialize.tpa~ // sets up various script variables and detectable spells
INCLUDE ~%MOD_FOLDER%/lib/fragments/setup_items.tph~
INCLUDE ~%MOD_FOLDER%/lib/fragments/setup_cre.tph~
COPY ~%MOD_FOLDER%/sto~ ~override~

// Copy animations
COPY ~%MOD_FOLDER%/animations/astral_demilich~ ~override~
COPY ~%MOD_FOLDER%/animations/black_weaver~ ~override~
COPY ~%MOD_FOLDER%/animations/couatl~ ~override~
COPY ~%MOD_FOLDER%/animations/displacer_beast~ ~override~
COPY ~%MOD_FOLDER%/animations/driders~ ~override~
COPY ~%MOD_FOLDER%/animations/green_rakshasa~ ~override~
COPY ~%MOD_FOLDER%/animations/hamatula~ ~override~
COPY ~%MOD_FOLDER%/animations/pit_fiend~ ~override~
COPY ~%MOD_FOLDER%/animations/red_worg~ ~override~

// Fix antimagic BP2 spell so it prevents Simulacrum from being cast

//////////////////////////////////////////////////////////////////////////////////////////////////////
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cutscene~ USING ~%MOD_FOLDER%/lang/%LANGUAGE%/cutscenes.tra~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bp2scripts~
COMPILE ~%MOD_FOLDER%/ohbvic01~ /* editing the item_roll at the bottom to be zero so we ALWAYS get loot */ /* using EVALUATE BUFFER here means it won't install where going YOLO */

// bucket's tp2 additions - still need to be approved and tested

/* new journal entries */
ADD_JOURNAL TITLE (@800000) @800001 USING ~%MOD_FOLDER%/lang/english/JournalEntries.tra~

/* add new vampire creature for it */
COPY ~%MOD_FOLDER%/creatures/vampirecutscene/zbvamp.cre~ ~override/zbvamp.cre~ /* need to move this line over to the setup-cre file but i'm not touching that since it's royal's baby */
WRITE_ASCII DEATHVAR ~zbvamp~ #32

/* compile the new dialogues and recompile old ones with the new strings */
/* do we just recompile ALL 54 dialogues? only added a few that i know have been edited, there are more i need to add */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dlg/ohbbello.d~
                        ~%MOD_FOLDER%/dlg/ohbdenna.d~ /* still needs HEAVY edits */
                        ~%MOD_FOLDER%/dlg/ohbjoker.d~
                        ~%MOD_FOLDER%/dlg/ohbnajim.d~
                        ~%MOD_FOLDER%/dlg/ohbbrodl.d~ /* still needs HEAVY edits */
                        ~%MOD_FOLDER%/dlg/ohbstann.d~
                        ~%MOD_FOLDER%/dlg/ohbtartl.d~
                        ~%MOD_FOLDER%/dlg/ohbcook.d~
                        ~%MOD_FOLDER%/dlg/ohbcurel.d~
                        ~%MOD_FOLDER%/dlg/ohbdulf.d~
                        ~%MOD_FOLDER%/dlg/ohbelgia.d~
                        ~%MOD_FOLDER%/dlg/ohbfeldr.d~
                        ~%MOD_FOLDER%/dlg/ohbgc01.d~
                        ~%MOD_FOLDER%/dlg/ohbgerro.d~
                        ~%MOD_FOLDER%/dlg/ohbgezzt.d~
                        ~%MOD_FOLDER%/dlg/ohbimim.d~ /* needs crossmod edits for BPinBG1 */
                        ~%MOD_FOLDER%/dlg/ohblaris.d~
                        ~%MOD_FOLDER%/dlg/ohbleoka.d~
                        ~%MOD_FOLDER%/dlg/ohbmardu.d~
                        ~%MOD_FOLDER%/dlg/ohbvoghi.d~
                        ~%MOD_FOLDER%/dlg/ohbstirv.d~

/* copy edited sound files over */
COPY ~%MOD_FOLDER%/wav/oh10~ ~override~
COPY ~%MOD_FOLDER%/wav/oh88~ ~override~
COPY ~%MOD_FOLDER%/wav/oh90~ ~override~
COPY ~%MOD_FOLDER%/wav/oh91~ ~override~
COPY ~%MOD_FOLDER%/wav/oh92~ ~override~
COPY ~%MOD_FOLDER%/wav/oh93~ ~override~
COPY ~%MOD_FOLDER%/wav/oh94~ ~override~
COPY ~%MOD_FOLDER%/wav/oh95~ ~override~
COPY ~%MOD_FOLDER%/wav/oh96~ ~override~
COPY ~%MOD_FOLDER%/wav/zb~ ~override~

/* add messenger timer */
COPY_EXISTING ~BALDUR.bcs~ ~%workspace%/~
ACTION_IF NOT FILE_CONTAINS ~%workspace%/BALDUR.bcs~ ~ZBPodMessengerSpawned~
THEN BEGIN
  EXTEND_BOTTOM ~BALDUR.bcs~ ~%MOD_FOLDER%/scripts/zbmes.baf~
END


/* make eshpel conversable */
OUTER_SET label = RESOLVE_STR_REF(~Converse~)

APPEND ~ITEMDIAL.2da~
~zbeshpeu %label% zbeshpeu~
UNLESS ~zbeshpeu~

// Nephetel
INCLUDE ~%MOD_FOLDER%/nephetel/core.tpa~

COMPILE ~%MOD_FOLDER%/test/zbeshpeu.d~ /* esphel dialogues */
        ~%MOD_FOLDER%/test/zbvyan.d~ /* vyantri's new line */
        ~%MOD_FOLDER%/test/zbvci.d~ /* vanilla npc interjections */
        ~%MOD_FOLDER%/test/gaelan.d~ /* add a line to gaelan */
        ~%MOD_FOLDER%/test/crossmod.d~ /* crossmod - for now only BGinBG1 */

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @20003 DESIGNATED 9 LABEL ~zb_keldorn_simp_nephetel_portrait~ // ADD ALT PORTRAITS
REQUIRE_COMPONENT "zbpod.tp2" "2" @30000 // traify later
SUBCOMPONENT ~Nephetel Alternate Portraits~ // traify later
//////////////////////////////////////////////////////////////////////////////////////////////////////

COPY ~%MOD_FOLDER%/altportrait/keldornsimp_zbnephy.bmp~ ~override/zbnephy.bmp~

//////////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @20004 DESIGNATED 10 LABEL ~zb_llewren_nephetel_portrait~ // ADD ALT PORTRAITS
REQUIRE_COMPONENT "zbpod.tp2" "2" @30000 // traify later
SUBCOMPONENT ~Nephetel Alternate Portraits~ // traify later
//////////////////////////////////////////////////////////////////////////////////////////////////////

COPY ~%MOD_FOLDER%/altportrait/llew_zbnephy.bmp~ ~override/zbnephy.bmp~

//////////////////////////////////////////////////////////////////////////////////////////////////////
