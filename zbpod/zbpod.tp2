BACKUP "weidu_external/zbpod/backup/"
SUPPORT "https://www.gibberlings3.net/forums/forum/28-miscellaneous-released-mods/"
VERSION "0.1"
README ~%MOD_FOLDER%/README.html~
AUTO_EVAL_STRINGS
/////////////////////////////////////////////////////////////////////////////////////////////////////

AUTO_TRA ~%MOD_FOLDER%/lang/%s~
ALWAYS
  INCLUDE ~%MOD_FOLDER%/lib/a7#add_kit_ex.tpa~
  INCLUDE ~%MOD_FOLDER%/lib/alter_script.tpa~
  INCLUDE ~%MOD_FOLDER%/lib/functions.tph~
  INCLUDE ~%MOD_FOLDER%/lib/royalprotector/zs_royalfunctions.tpa~
END


AUTO_TRA ~%MOD_FOLDER%/lang/%s~
LANGUAGE  ~English~
          ~english~
          ~zbpod/lang/english/game.tra~
          ~zbpod/lang/english/weidu.tra~
          ~zbpod/lang/english/dialogs.tra~
          ~zbpod/lang/english/JournalEntries.tra~
          ~zbpod/lang/english/cutscenes.tra~

LANGUAGE  ~Simplified Chinese By Lzw104522773~
          ~schinese~
          ~zbpod/lang/schinese/game.tra~
          ~zbpod/lang/schinese/weidu.tra~
          ~zbpod/lang/schinese/dialogs.tra~
          ~zbpod/lang/schinese/JournalEntries.tra~
          ~zbpod/lang/schinese/cutscenes.tra~


//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @20001 DESIGNATED 2 LABEL ~zb_pod_ee~ // INSTALL PIT OF DESPAIR FOR ENHANCED EDITION
REQUIRE_PREDICATE GAME_IS "bg2ee eet" @20050
//////////////////////////////////////////////////////////////////////////////////////////////////////

COPY ~%MOD_FOLDER%/spells/bp2~ ~override~
COPY ~%MOD_FOLDER%/movie/pod.wbm~ ~movies~ /* adding directly to movies, not override, because otherwise it doesn't work. this isn't ideal. */

ACTION_IF NOT FILE_EXISTS_IN_GAME ~p_cta1.bam~ THEN BEGIN
INCLUDE ~%MOD_FOLDER%/lib/animations.tpa~
END

INCLUDE ~%MOD_FOLDER%/lib/fragments/setup_items.tph~
INCLUDE ~%MOD_FOLDER%/lib/fragments/setup_cre.tph~
INCLUDE ~%MOD_FOLDER%/lib/fragments/setup_stores.tph~

// Fix antimagic BP2 spell so it prevents Simulacrum from being cast

//////////////////////////////////////////////////////////////////////////////////////////////////////
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cutscene~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bp2scripts~
COMPILE ~%MOD_FOLDER%/ohbvic01~ /* editing the item_roll at the bottom to be zero so we ALWAYS get loot */ /* using EVALUATE BUFFER here means it won't install where going YOLO */

// Add Portrait Icons
COPY ~%MOD_FOLDER%/spells/picon~ ~override~

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@18000) STR_VAR bam_file = "zbantisi" RET icon END
COPY_EXISTING ~zbpdsim.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = icon END

// bucket's tp2 additions - still need to be approved and tested

/* new journal entries */
ADD_JOURNAL TITLE (@800000) @800001 USING ~%MOD_FOLDER%/lang/english/JournalEntries.tra~

/* add new vampire creature for it */
COPY ~%MOD_FOLDER%/creatures/vampirecutscene/zbvamp.cre~ ~override/zbvamp.cre~ /* need to move this line over to the setup-cre file but i'm not touching that since it's royal's baby */
WRITE_ASCII DEATHVAR ~zbvamp~ #32

/* compile the new dialogues and recompile old ones with the new strings */
/* do we just recompile ALL 54 dialogues? only added a few that i know have been edited, there are more i need to add */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dlg/ohbbello.d~
                        ~%MOD_FOLDER%/dlg/ohbdenna.d~ /* still needs HEAVY edits */
                        ~%MOD_FOLDER%/dlg/ohbjoker.d~
                        ~%MOD_FOLDER%/dlg/ohbnajim.d~
                        ~%MOD_FOLDER%/dlg/ohbbrodl.d~ /* still needs HEAVY edits */
                        ~%MOD_FOLDER%/dlg/ohbstann.d~
                        ~%MOD_FOLDER%/dlg/ohbtartl.d~
                        ~%MOD_FOLDER%/dlg/ohbcook.d~
                        ~%MOD_FOLDER%/dlg/ohbcurel.d~
                        ~%MOD_FOLDER%/dlg/ohbdulf.d~
                        ~%MOD_FOLDER%/dlg/ohbelgia.d~
                        ~%MOD_FOLDER%/dlg/ohbfeldr.d~
                        ~%MOD_FOLDER%/dlg/ohbgc01.d~
                        ~%MOD_FOLDER%/dlg/ohbgerro.d~
                        ~%MOD_FOLDER%/dlg/ohbgezzt.d~
                        ~%MOD_FOLDER%/dlg/ohbimim.d~ /* needs crossmod edits for BPinBG1 */
                        ~%MOD_FOLDER%/dlg/ohblaris.d~
                        ~%MOD_FOLDER%/dlg/ohbleoka.d~
                        ~%MOD_FOLDER%/dlg/ohbmardu.d~
                        ~%MOD_FOLDER%/dlg/ohbvoghi.d~
                        ~%MOD_FOLDER%/dlg/ohbstirv.d~

/* copy edited sound files over */
COPY ~%MOD_FOLDER%/wav~ ~override~

/* cutscene to transfer to OH8000 */
COMPILE ~%MOD_FOLDER%/test/podstr.baf~

/* add messenger timer */
EXTEND_BOTTOM ~BALDUR.bcs~ ~%MOD_FOLDER%/test/zbmes.baf~
/* add messenger script */
COMPILE ~%MOD_FOLDER%/test/zbme2.baf~
/* add messenger creature */
COPY ~%MOD_FOLDER%/creatures/messenger/zbmes.cre~ ~override/zbmes.cre~
SAY NAME1 ~Mysterious Messenger~
SAY NAME2 ~Mysterious Messenger~
WRITE_ASCII 0x248 ~zbme2~ #8  // override script
WRITE_ASCII 0x2cc ~zbmes~  #8  // dialogue file

/* make eshpel conversable */
OUTER_SET label = RESOLVE_STR_REF(~Converse~)

APPEND ~ITEMDIAL.2da~
~zbeshpeu %label% zbeshpeu~
UNLESS ~zbeshpeu~

COMPILE ~%MOD_FOLDER%/test/zbeshpeu.d~ /* esphel dialogues */
		~%MOD_FOLDER%/test/zbvyan.d~ /* vyantri's new line */
		~%MOD_FOLDER%/test/zbmes.d~ /* messenger - aka best girl in disguise lines */
        ~%MOD_FOLDER%/test/zbvci.d~ /* vanilla npc interjections */
		~%MOD_FOLDER%/test/gaelan.d~ /* add a line to gaelan */
		~%MOD_FOLDER%/test/zbneph.d~ /* nephetel's base dialogue file */
		~%MOD_FOLDER%/test/crossmod.d~ /* crossmod - for now only BGinBG1 */

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @20002 DESIGNATED 8 LABEL ~zb_pod_rm_portraits~ // REMOVE PORTRAITS
REQUIRE_COMPONENT "zbpod.tp2" "2" @30000 // traify later
//////////////////////////////////////////////////////////////////////////////////////////////////////

COPY_EXISTING ~zbdrclch.cre~ ~override~
              ~ohbbrodl.cre~ ~override~
              ~ohbbello.cre~ ~override~
              ~ohbtartl.cre~ ~override~
              ~ohbstirv.cre~ ~override~
              ~ohbwing.cre~ ~override~

	WRITE_ASCII 0x34 ~~ #8
	WRITE_ASCII 0x3c ~~ #8
BUT_ONLY

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @20003 DESIGNATED 9 LABEL ~zb_pod_add_alt_portraits~ // ADD ALT PORTRAITS
REQUIRE_COMPONENT "zbpod.tp2" "2" @30000 // traify later
//////////////////////////////////////////////////////////////////////////////////////////////////////

COPY ~%MOD_FOLDER%/altportrait~ ~override~

//////////////////////////////////////////////////////////////////////////////////////////////////////
